// v1.2.0
(
    baseTable as function,
    keyColumns as list,
    dataCtor as function,
    descriptor as record,
    optional additionalHandlers as record
) as table =>
    let
        transformDescriptor = (key, value) =>
            let
                map = [
                    Name = "NavigationTable.NameColumn",
                    Data = "NavigationTable.DataColumn",
                    Tags = "NavigationTable.TagsColumn",
                    ItemKind = "NavigationTable.ItemKindColumn",
                    ItemName = "Preview.DelayColumn",
                    IsLeaf = "NavigationTable.IsLeafColumn"
                ]
            in
                if value is list then
                    [Name=value{0}, Ctor=value{1}, MetadataName = Record.FieldOrDefault(map, key)]
                else if value is text then
                    [Name=value, Ctor=null, MetadataName = Record.FieldOrDefault(map, key)]
                else if value is function then
                    [Name=key, Ctor=value, MetadataName = Record.FieldOrDefault(map, key)]
                else
                    error "Unexpected descriptor value for '" & key & "'",
        fields = List.Combine({
            List.Transform(keyColumns, (key) => [Name=key, Ctor=(row) => Record.Field(row, key), MetadataName=null]),
            if Record.HasFields(descriptor, {"Data"}) then {} else
                {transformDescriptor("Data", (row) => Function.Invoke(dataCtor, Record.ToList(Record.SelectFields(row, keyColumns))))},
            Table.TransformRows(Record.ToTable(descriptor), each transformDescriptor([Name], [Value]))
        }),
        metadata = List.Accumulate(fields, [], (m, d) => let n = d[MetadataName] in if n = null then m else Record.AddField(m, n, d[Name])),
        withoutMetadataOnly = List.Select(fields, each _[Ctor] <> null),
        tableKeys = List.Transform(withoutMetadataOnly, each [Name]),
        tableValues = List.Transform(withoutMetadataOnly, each [Ctor]),
        tableType = Type.ReplaceTableKeys(
            type table Type.ForRecord(List.Accumulate(tableKeys, [], (r, f) => Record.AddField(r, f, [Type=if f = "Data" then type table else type any, Optional=false])), false),
            {[Columns=keyColumns, Primary=true]}
        ) meta metadata,
        reduceAnd = (ast) => if ast[Kind] = "Binary" and ast[Operator] = "And" then List.Combine({@reduceAnd(ast[Left]), @reduceAnd(ast[Right])}) else {ast},
        matchFieldAccess = (ast) => if ast[Kind] = "FieldAccess" and ast[Expression] = RowExpression.Row then ast[MemberName] else ...,
        matchConstant = (ast) => if ast[Kind] = "Constant" then ast[Value] else ...,
        matchIndex = (ast) => if ast[Kind] = "Binary" and ast[Operator] = "Equals"
            then
                if ast[Left][Kind] = "FieldAccess"
                    then Record.AddField([], matchFieldAccess(ast[Left]), matchConstant(ast[Right]))
                    else Record.AddField([], matchFieldAccess(ast[Right]), matchConstant(ast[Left]))
            else ...,
        lazyRecord = (recordCtor, keys, baseRecord) =>
            let record = recordCtor() in List.Accumulate(keys, [], (r, f) => Record.AddField(r, f, () => (Record.FieldOrDefault(baseRecord, f, null) ?? Record.Field(record, f)), true)),
        getIndex = (selector, keys) => Record.SelectFields(Record.Combine(List.Transform(reduceAnd(RowExpression.From(selector)), matchIndex)), keys),
        viewException.IsMarked = (errorRecord) => Record.FieldCount( Value.Metadata( Value.Type(errorRecord[Reason]?) ) ) > 0
    in
        Table.View(null, [
            GetType = () => tableType,
            GetRows = () => #table(tableType, List.Transform(Table.ToRecords(baseTable()), (row) => List.Transform(tableValues, (ctor) => ctor(row)))),
            OnSelectRows = (selector) =>
                let
                    index = try getIndex(selector, keyColumns) otherwise [],
                    default = Table.SelectRows(GetRows(), selector),
                    triedData = try Function.Invoke(dataCtor, Record.ToList(index)),
                    dataErrorIsViewException = if triedData[HasError] then viewException.IsMarked(triedData[Error]) else false
                in
                    if (Record.FieldCount(index) <> List.Count(keyColumns)) then
                        Diagnostics.Trace(TraceLevel.Information, [Name = "NavigationTableView/OnSelectRows/FoldingFailure", Data = [], SafeData = [DueToError = triedData[HasError]]], default)
                    else if (triedData[HasError]) then
                        if dataErrorIsViewException then
                            // In case the error is a Table.View error, we need to stop processing and return the error directly instead of invoking the default handler.
                            Diagnostics.Trace(TraceLevel.Information, [Name = "NavigationTableView/OnSelectRows/FoldingFailure", Data = [], SafeData = [HasViewException = true]], Table.FromRecords({
                                index & lazyRecord(
                                    () => Table.First(default),
                                    List.Skip(tableKeys, Record.FieldCount(index)),
                                    Record.AddField([], "Data", () => error triedData[Error], /*delayed*/ true)) // we are delaying the throw of the error when Data is actually accessed.
                                },
                                tableType))
                        else
                            // If the error is not a Table.View error, use the default handler.
                            Diagnostics.Trace(TraceLevel.Error, [Name = "NavigationTableView/OnSelectRows/FoldingFailure", Data = [], SafeData = [HasViewException = false]], default)
                    else 
                        Table.FromRecords(
                            {
                                index & lazyRecord(
                                    () => Table.First(default),
                                    List.Skip(tableKeys, Record.FieldCount(index)),
                                    Record.AddField([], "Data", triedData[Value])) // there is no need to delay because triedData is already evaluated.
                            },
                            tableType)
        ] & (additionalHandlers ?? []))